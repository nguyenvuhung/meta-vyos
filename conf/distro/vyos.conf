# This is VyOS for OpenEmbedded
DISTRO = "vyos"
DISTRO_NAME = "OE-VyOS - Network Operating System"
DISTRO_VERSION = "0.1"
DISTRO_CODENAME = "none"

# This layer contains kernel patches for a variety of machine-specific
# kernels using .bbappend files in recipes-kernel/linux. To allow image
# building without all machine layers being present, we turn 'dangling
# .bbappend file' errors into warnings
BB_DANGLINGAPPENDS_WARNONLY = "1"

# we install dpkg anyways, might as well use .deb packages instead of the
# default .ipk
PACKAGE_CLASSES = "package_deb"

# VyOS configuration requires fuse filesystem to work, so autoload module on
# system bootup
KERNEL_MODULE_AUTOLOAD += " fuse"

# mirror VyOS's original settings (Yocto uses /home/root as default)
ROOT_HOME = "/root"

# VyOS shall use systemd
VIRTUAL-RUNTIME_init_manager = "systemd"
VIRTUAL-RUNTIME_initscripts = "systemd-compat-units"
DISTRO_FEATURES_append = " systemd"

# required when having all kernel modules inside the initramfs ...
INITRAMFS_MAXSIZE ?= "262144"

# Disable unnecessary DISTRO_FEATURES and MACHINE_FEATURES here
DISTRO_FEATURES_remove = " \
    alsa \
    bluetooth \
    cramfs \
    directfb \
    irda \
    nfs \
    opengl \
    pcmcia \
    smbfs \
    usbgadget \
    wayland \
    x11 \
    nfc \
    3g \
    "

MACHINE_FEATURES_remove = " \
    alsa \
    bluetooth \
    irda \
    pcmcia \
    phone \
    qvga \
    touchscreen \
    usbgadget \
    "

DISTRO_FEATURES_append = " \
    pam \
    ldap \
    vyos-core \
    vyos-config \
    vyos-net \
    vyos-wlan \
    vyos-routing \
    vyos-wwan \
    vyos-vpn \
    vyos-qos \
    vyos-pppoe \
    vyos-dhcp \
    vyos-monitor \
    vyos-firewall \
    vyos-redundancy \
    vyos-proxy \
    vyos-debug \
    "

DISTRO_FEATURES_append_x86 = " vyos-non-embedded"
DISTRO_FEATURES_append_x86-64 = " vyos-non-embedded"

DISTRO_FEATURES_append_arm = " \
    vyos-embedded \
    vyos-swupdate \
    "

# Image level user/group configuration.
# Inherit extrausers to make the setting of EXTRA_USERS_PARAMS effective.
INHERIT += "extrausers"

EXTRA_USERS_PARAMS = "\
    groupadd -r vyattacfg; \
    groupadd -r vyattaop; \
    groupadd -r quaggavty; \
    groupadd -r fuse; \
    useradd \
        --home /home/vyos \
        --system \
        --gid users \
        --shell /bin/vbash \
        --groups adm,sudo,dip,disk,vyattacfg,quaggavty,fuse \
        --password '' \
        vyos; \
    usermod -P vyos vyos; \
    useradd \
        --home /var/run/quagga \
        --system \
        --user-group \
        --shell /bin/false \
        --comment Vyatta_Quagga_routing_suite \
        --password '' \
        quagga; \
    "

# Ideally, all VyOS builds should use the same kernel, however
# this goal may not be easily attainable for embedded platforms that
# require custom patches or newer kernels
PREFERRED_PROVIDER_virtual/kernel = "linux-vyos"
